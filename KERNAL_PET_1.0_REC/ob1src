.PAG 'MESSAGES & IEEE'
MSG	LDA MS1,Y
	PHP
	AND #$7F
	JSR PRT
	INY
	PLP
	BPL MSG
	RTS
	*=$7000+OFFSET  ;SOFT OFFSET
MS1	.BYT 'TOO MANY FILE',$D3
MS2	.BYT 'FILE OPE',$CE
MS3	.BYT 'FILE NOT OPE',$CE
MS4	.BYT 'FILE NOT FOUN',$C4
MS5	.BYT $D,'SEARCH'
MS20	.BYT 'ING',$A0
MS6	.BYT 'FOR',$A0
MS7	.BYT $D,'PRESS PLAY',$A0
MS8	.BYT '& RECORD',$A0
MS9	.BYT 'ON TAPE ',$A3
MS10	.BYT $D
MS22	.BYT 'LOA',$C4
MS11	.BYT $D,'WRITING',$A0
MS21	.BYT $D
MS12	.BYT 'VERIF',$D9
MS13	.BYT 'DEVICE NOT PRESEN',$D4
MS15	.BYT 'NOT INPUT FIL',$C5
MS16	.BYT 'NOT OUTPUT FIL',$C5
MS17	.BYT $D,'FOUND',$A0
MS18	.BYT $D,'OK',$8D
MS19	.BYT $D,'READY.',$8D
TALK	LDA #$40
	BNE LIST1
LISTN	LDA #' 
LIST1	PHA
	LDA PIA
	ORA #2
	STA PIA
	LDA #$3C
	STA IEEIS
	BIT C3P0
	BEQ LIST2
	LDA #$34
	STA PIAL1
	JSR ISOUR
	LDA #0
	STA C3P0
	LDA #$3C
	STA PIAL1
LIST2	PLA
	ORA FA
	STA BSOUR
LIST4	LDA PIA
	BPL LIST4
	AND #$FB
	STA PIA
ISOUR	LDA #$3C
	STA IEEOS
	LDA PIA
	AND #$41
	CMP #$41
	BEQ ERRP7
	LDA BSOUR
	EOR #$FF
	STA IEEO
ISR1	BIT PIA
	BVC ISR1
	LDA #$34
	STA IEEOS
ISR0	LDA #$FF
	STA TIH
ISR2	LDA PIA
	BIT IFR
	BVS ERRP0
	LSR A
	BCC ISR2
ISR3	LDA #$3C
	STA IEEOS
	LDA #$FF
	STA IEEO
	RTS
SECND	STA BSOUR
	JSR ISOUR
SCATN	LDA PIA
	ORA #4
	STA PIA
	RTS
ERRP0	LDA #1
ERR01	JSR UDST
	BNE ISR3
ERRP7	LDA #$80
	BMI ERR01
ERRP1	LDA #2
	JSR UDST
ER001	LDA PIA
	AND #$FD
	STA PIA
	LDA #$34
	STA IEEIS
	LDA #$0D
	RTS
TKSA	STA BSOUR
	JSR ISOUR
TKATN	JSR ER001
	JMP SCATN
CIOUT	BIT C3P0
	BMI CI2
	DEC C3P0
	BNE CI4
CI2	PHA
	JSR ISOUR
	PLA
CI4	STA BSOUR
	RTS
UNTLK	LDA #$5F
	BNE UNLSN1
UNLSN	LDA #$3F
UNLSN1	STA FA
	JSR LIST1
	BNE SCATN
ACPTR	LDA #$34
	STA IEEIS
	LDA PIA
	ORA #2
	STA PIA
ACP00	LDA #$FF
	STA TIH
ACP01	BIT IFR
	BVS ERRP1
	BIT PIA
	BMI ACP01
	LDA PIA
	AND #$FD
	STA PIA
	BIT PIAL
	BVS ACP03
	LDA #$40
	JSR UDST
ACP03	LDA IEEI
	EOR #$FF
	PHA
	LDA #$3C
	STA IEEIS
ACP05	BIT PIA
	BPL ACP05
	LDA #$34
	STA IEEIS
	PLA
	RTS
GETIN	LDA #0
	STA SATUS
	LDA DFLTN
	BNE BN10
	LDA NDX
	BEQ BN32
	SEI
	JMP LP2
BASIN	LDA DFLTN
	BNE BN10 .
	LDA PNTR
	STA LSTP
	LDA TBLX
	STA LSXP
	JMP LOOP5
BN10	CMP #3
	BNE BN20
	STA CRSW
	LDA LNMX
	STA INDX
	JMP LOOP5
BN20	BCS BN30
	STX XSAV
JTGET	JSR JTP20
	BNE JTG10
	JSR RBLK
	LDY #0
	TYA
	LDX FA
	STA BUFPT-1,X
	JMP JTGET
JTG10	LDA (TBUF)Y
	BNE JTG35
	LDA #64
	JSR UDST
	BNE JTGET
JTG35	LDX XSAV
	RTS
BN30	LDA SATUS
	BEQ BN35
BN32	RTS
BN35	JMP ACPTR
BSOUT	PHA
	LDA DFLTO
	BNE *+5
	JMP TYPERR
	CMP #3
	BNE BO10
	PLA
	JMP PRT
BO10	BMI BO20
	PLA
	JMP CIOUT
BO20	PLA
BO21	STA T1
	CMP #$1D
	BNE LF253
	INC L026A
	BEQ BN32
LF253	CMP #$A
	BEQ BN32
	PHA
	TXA
	PHA
	TYA
	PHA
	JSR JTP20
	BNE JTP10
	JSR WBLK
	LDX FA
	LDA #1
	STA BUFPT-1,X
	JSR LF5E3
	LDA #BDF
	STA (TBUF)Y
	INY
JTP10	LDA T1
	STA (TBUF)Y
RSTOR	PLA
	TAY
	PLA
	TAX
	PLA
	RTS
CLRCHN
CLRCH	LDA DFLTO
	BEQ JX750
	CMP #3
	BEQ JX750
	BMI JX750
	JSR UNLSN
JX750	LDA DFLTN
	BEQ JX770
	CMP #3
	BEQ JX770
	BMI JX770
	JSR UNTLK
JX770	LDA #0
	STA DFLTN
	LDA #3
	STA DFLTO
	RTS
CLALL	LDA #0
	STA LDTND
	BEQ CLRCHN
JLTLK	LDX LDTND
JX600	DEX
	BMI JZ101
	CMP LAT,X
	BEQ JZ101
	BNE JX600
JZ100	LDA LAT,X
	STA LA
	LDA FAT,X
	STA FA
	LDA SAT,X
	STA SA
JZ101	RTS
.FILE OB2SRC
