.PAG 'CODE6'
TIMELP	STY FBUFPT
	JSR TIMNUM
	JSR MUL10
	INC FBUFPT
	LDY FBUFPT
	JSR TIMNUM
	JSR MOVAF
	TAX
	BEQ NOML6
	INX
	TXA
	JSR FINML6
NOML6	LDY FBUFPT
	INY
	CPY #6
	BNE TIMELP
	JSR MUL10
	JSR QINT
	LDX #2
	SEI
TIMEST	LDA FACMOH,X
	STA CTIMR,X
	DEX
	BPL TIMEST
	CLI
	RTS
TIMNUM	LDA (INDEX)Y
	JSR QNUM
	BCC GOTNUM
	JMP FCERR
GOTNUM	SBC #$2F
	JMP FINLOG
GETSPT	LDY #2
	LDA (FACMO)Y
	CMP FRETOP+1
	BCC DNTCPY
	BNE QVARIA
	DEY
	LDA (FACMO)Y
	CMP FRETOP
	BCC DNTCPY
QVARIA	LDY FACLO
	CPY VARTAB+1
	BCC DNTCPY
	BNE COPY
	LDA FACMO
	CMP VARTAB
	BCS COPY
DNTCPY	LDA FACMO
	LDY FACMO+1
	JMP COPYC
COPY	LDY #0
	LDA (FACMO)Y
	JSR STRINI
	LDA DSCPNT
	LDY DSCPNT+1
	STA STRNG1
	STY STRNG1+1
	JSR MOVINS
	LDA #<DSCTMP
	LDY #>DSCTMP
COPYC	STA DSCPNT
	STY DSCPNT+1
	JSR FRETMS
	LDY #0
	LDA (DSCPNT)Y
	STA (LSTPNT)Y
	INY
	LDA (DSCPNT)Y
	STA (LSTPNT)Y
	INY
	LDA (DSCPNT)Y
	STA (LSTPNT)Y
	RTS
.SKI 5
PRINTN	JSR CMD
	JMP IODONE
CMD	JSR GETBYT
	BEQ SAVEIT
	LDA #44
	JSR SYNCHR
SAVEIT	PHP
	JSR COOUT
	STX CHANNL
	PLP
	JMP PRINT
STRDON	JSR STRPRT
NEWCHR	JSR CHRGOT
PRINT	BEQ CRDO
PRINTC	BEQ PRTRTS
	CMP #TABTK
	BEQ TABER
	CMP #SPCTK
	BEQ TABER
	CMP #44
	BEQ COMPRT
	CMP #59
	BEQ NOTABR
	JSR FRMEVL
	BIT VALTYP
	BMI STRDON
	JSR FOUT
	JSR STRLIT
	JSR STRPRT
	JSR CRTSKP
	BNE NEWCHR
FININL	LDY #0
	STY BUF,X
ZZ5=BUF-1
	LDX #<ZZ5
	LDA CHANNL
	BNE PRTRTS
CRDO	LDA CHANNL
	BNE LC9D8
	STA TRMPOS
LC9D8	LDA #13
	JSR OUTDO
	LDA #$0A        ;LINE FEED
	JSR OUTDO
CRFIN	LDA CHANNL
	BNE PRTRTS
	TXA
	PHA
	LDX NULCNT
	BEQ CLRPOS
	LDA #0
PRTNUL	JSR OUTDO
	DEX
	BNE PRTNUL
CLRPOS	STX TRMPOS
	PLA
	TAX
PRTRTS	RTS
COMPRT	LDA TRMPOS
NCMPOS	=@36
	SEC
MORCO1	SBC #CLMWID
	BCS MORCO1
	EOR #255
	ADC #1
	BNE ASPAC
TABER	PHA
	JSR GTBYTC
	CMP #41
	BNE SNERR4
	PLA
	CMP #TABTK
	BNE XSPAC1
	TXA
	SBC TRMPOS
	BCC NOTABR
	BEQ NOTABR
ASPAC	TAX
XSPAC1	JSR CRTSKP
XSPAC2	DEX
	BNE XSPAC1
NOTABR	JSR CHRGET
	JMP PRINTC
.FIL CODE7
