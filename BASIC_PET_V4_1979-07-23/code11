;*****CODE11*****
	CMP (DSCTMP+1)Y
	BEQ NXTCMP
	LDX #$FF
	BCS DOCMP
	LDX #1
DOCMP	INX
	TXA
	ROL A
	AND DOMASK
	BEQ GOFLOT
	LDA #$FF
GOFLOT	JMP FLOAT
DIM3	JSR CHKCOM
DIM	TAX
	JSR PTRGT1
	JSR CHRGOT
	BNE DIM3
	RTS
PTRGET	LDX #0
	JSR CHRGOT
PTRGT1	STX DIMFLG
PTRGT2	STA VARNAM
	JSR CHRGOT
	JSR ISLETC
	BCS PTRGT3
INTERR	JMP SNERR
PTRGT3	LDX #0
	STX VALTYP
	STX INTFLG
	JSR CHRGET
	BCC ISSEC
	JSR ISLETC
	BCC NOSEC
ISSEC	TAX
EATEM	JSR CHRGET
	BCC EATEM
	JSR ISLETC
	BCS EATEM
NOSEC	CMP #'$
	BNE NOTSTR
	LDA #$FF
	STA VALTYP
	BNE TURNON
NOTSTR	CMP #'%
	BNE STRNAM
	LDA SUBFLG
	BNE INTERR
	LDA #128
	STA INTFLG
	ORA VARNAM
	STA VARNAM
TURNON	TXA
	ORA #128
	TAX
	JSR CHRGET
STRNAM	STX VARNAM+1
	SEC
	ORA SUBFLG
	SBC #40
	BNE *+5
	JMP ISARY
	LDY #0
	STY SUBFLG
	LDA VARTAB
	LDX VARTAB+1
STXFND	STX LOWTR+1
LOPFND	STA LOWTR
	CPX ARYTAB+1
	BNE LOPFN
	CMP ARYTAB
	BEQ NOTFNS
LOPFN	LDA VARNAM
	CMP (LOWTR)Y
	BNE NOTIT
	LDA VARNAM+1
	INY
	CMP (LOWTR)Y
	BNE NXTPTR
	JMP FINPTR
NXTPTR	DEY
NOTIT	CLC
	LDA LOWTR
	ADC #6+ADDPRC
	BCC LOPFND
	INX
	BNE STXFND
ISLETC	CMP #'A
	BCC ISLRTS
	SBC #$5B
	SEC
	SBC #@245
ISLRTS	RTS
NOTFNS	PLA
	PHA
ZZ6=ISVRET-1
	CMP #<ZZ6
	BNE NOTEVL
LDZR	LDA #<ZERO
	LDY #>ZERO
	RTS
NOTEVL	LDA VARNAM
	LDY VARNAM+1
	CMP #'T
	BNE QSTAVR
	CPY #@311
	BEQ LDZR
	CPY #@111
	BNE QSTAVR
GOBADV	JMP SNERR
QSTAVR	CMP #'S
	BNE QDSVAR
	CPY #'T
	BEQ GOBADV
QDSVAR	CMP #'D         ;DISK STATUS?
	BNE VAROK
	CPY #'S
	BEQ GOBADV      ;CANNOT MODIFY D5
	CPY #$D3
	BEQ GOBADV      ;CANNOT MODIFY DS$
VAROK	LDA ARYTAB
	LDY ARYTAB+1
	STA LOWTR
	STY LOWTR+1
	LDA STREND
	LDY STREND+1
	STA HIGHTR
	STY HIGHTR+1
	CLC
	ADC #6+ADDPRC
	BCC NOTEVE
	INY
NOTEVE	STA HIGHDS
	STY HIGHDS+1
	JSR BLTU
	LDA HIGHDS
	LDY HIGHDS+1
	INY
	STA ARYTAB
	STY ARYTAB+1
	STA ARYPNT      ;SET FOINTER TO ARRAYS
	STY ARYPNT+1
ARYVA2	LDA ARYPNT
	LDX ARYPNT+1
ARYVA3	CPX STREND+1    ;END OF ARRAYS ?
	BNE ARYVGO
	CMP STREND
	BEQ ARYDON      ;FINISHED...
ARYVGO	STA INDEX1      ;PARSE THE ARRAY
	STX INDEX1+1
	LDY #1-ADDPRC
	LDA (INDEX1),Y  ;ARRAY NAME
	TAX
	INY
	LDA (INDEX1),Y  ;NAME 2ND CHAR
	PHP             ;SAVE STATUS REG
	INY
	LDA (INDEX1),Y  ;POINT TO HEXT ARRAY
	ADC ARYPNT
	STA ARYPNT
	INY
	LDA (INDEX1),Y
	ADC ARYPNT+1
	STA ARYPNT+1
	PLP             ;RESTORE STATUS
	BPL ARYVA2      ;NOT A STRING TYPE
	TXA
	BMI ARYVA2      ;NOT A STRING ARRAY
	INY             ;OK WE HAYE A STRING ARRAY
	LDA (INDEX1),Y
	LDY #$00
	ASL A
	ADC #5
	ADC INDEX1
	STA INDEX1
	BCC ARYGET
	INC INDEX1+1
ARYGET	LDX INDEX1+1    ;PARSE ARRAY
	CPX ARYPNT+1    ;DONE WITH THIS ARRAY ?
	BNE GOGO
	CMP ARYPNT
	BEQ ARYVA3      ;YES...
GOGO	LDY #$00        ;PROCESS STRING FOINTER
	LDA (INDEX1),Y
	BEQ DVARTS      ;NO STRING...GO ON...
	INY
	CLC
	ADC (INDEX1),Y
	STA HIGHTR
	TAX             ;SAVE IS X FOR CHECK
	INY
	LDA (INDEX1),Y
	ADC #$00        ;ADJUST HIGH BYTE
	STA HIGHTR+1
	CMP FRETOP+1    ;NO FIX IF BELOW FRETOP
	BCC DVARTS
	BNE GOGO1
	CPX FRETOP
	BCC DVARTS
;FIX BACKWARDS POINTER BY ADDING
;MOVE LEHSTH TO IT
GOGO1	LDY #$00
	LDA (HIGHTR),Y
	ADC #6+ADDPRC-1 ;CARRY SET (CAREFUL)
	STA (HIGHTR),Y
	INY
	LDA (HIGHTR),Y
	ADC #$00
	STA (HIGHTR),Y  ;DONE WITH THIS STR.
;FIX THE NEXT STRING IN THE ARRAY
DVARTS	LDA #STRSIZ     ;BUMP BY FOUR6
	CLC
	ADC INDEX1
	STA INDEX1
	BCC ARYGET
	INC INDEX1+1
	BNE ARYGET      ;BRAHCH ALWAYS
;
ARYDON	LDY #0
	LDA VARNAM
	STA (LOWTR)Y
	INY
	LDA VARNAM+1
	STA (LOWTR)Y
	LDA #0
	INY
	STA (LOWTR)Y
	INY
	STA (LOWTR)Y
	INY
	STA (LOWTR)Y
	INY
	STA (LOWTR)Y
	INY
	STA (LOWTR)Y
FINPTR	LDA LOWTR
	CLC
	ADC #2
	LDY LOWTR+1
	BCC FINNOW
	INY
FINNOW	STA VARPNT
	STY VARPNT+1
	RTS
FMAPTR	LDA COUNT
	ASL A
	ADC #5
	ADC LOWTR
	LDY LOWTR+1
	BCC JSRGM
	INY
JSRGM	STA ARYPNT
	STY ARYPNT+1
	RTS
.FIL CODE12
