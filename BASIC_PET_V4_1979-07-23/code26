.PAG 'CODE26'
INITAT	INC CHRGET+7
	BNE CHDGOT
	INC CHRGET+8
CHDGOT	LDA 60000
	CMP #':
	BCS CHDRTS
	CMP #' 
	BEQ INITAT
	SEC
	SBC #'0
	SEC
	SBC #$D0
CHDRTS	RTS
	.BYT 128,79,199,82,88
INIT	LDX #STKEND-256
	TXS
	LDA #76
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	LDX #RNDX+4-CHRGET
MOVCHG	LDA INITAT-1,X
	STA CHRGET-1,X
	DEX
	BNE MOVCHG
	LDA #STRSIZ
	STA FOUR6
	TXA
	STA BITS
	STA CHANNL
	STA LASTPT+1
	STA DSDESC
	PHA
	INX
	STX BUF-3
	STX BUF-4
	LDX #TEMPST
	STX TEMPPT
	LDY #>RAMLOC
	STA TXTTAB
	STY TXTTAB+1
	STA LINNUM
	STY LINNUM+1
	TAY
.SKI
;CREATE RAM INITIALIZATION IF NEEDED
	LDA #>OFFSET
	BNE LOOPMM
	LDA #<ROMLOC
	LDY #>ROMLOC
	JMP USEDEF
LOOPMM	INC LINNUM
	BNE LOOPM1
	INC LINNUM+1
	BMI USEDEC
LOOPM1	LDA #85
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BNE USEDEC
	ASL A
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BEQ LOOPMM
USEDEC	LDA LINNUM
	LDY LINNUM+1
USEDEF	STA MEMSIZ
	STY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	LDY #0
	TYA
	STA (TXTTAB)Y
	INC TXTTAB
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JSR SCRTCH
	JMP READY
WORDS	.BYT ' BYTES FREE',13,0
FREMES	.BYT '### COMMODORE BASIC ###'
	.BYT 13,13,0
LASTWR
.END
