.PAG 'CODE26'
INITAT	INC CHRGET+7
	BNE CHDGOT
	INC CHRGET+8
CHDGOT	LDA 60000
	CMP #':
	BCS CHDRTS
	CMP #' 
	BEQ INITAT
	SEC
	SBC #'0
	SEC
	SBC #$D0
CHDRTS	RTS
	.BYT 128,79,199,82,89
INIT	LDX #255
	STX CURLIN+1
	TXS
	LDA #76
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	LDX #RNDX+4-CHRGET
MOVCHG	LDA INITAT-1,X
	STA CHRGET-1,X
	DEX
	BNE MOVCHG
	TXA
	STA BITS
	STA CHANNL
	STA LASTPT+1
	STA NULCNT
	STA TRMPOS
	PHA
	STA CNTWFL
	LDA #STRSIZ
	STA FOUR6
	LDA #',
	STA LINNUM+1
	JSR CRDO
	LDX #TEMPST
	STX TEMPPT
	LDA #<RAMLOC
	LDY #>RAMLOC
	STA LINNUM
	STY LINNUM+1
	LDY #0
.SKI
LOOPMM	INC LINNUM
	BNE LOOPM1
	INC LINNUM+1
	LDA LINNUM+1
	CMP #$80
	BEQ USEDEC
LOOPM1	LDA #146
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BNE USEDEC
	ASL A
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BEQ LOOPMM
USEDEC	LDA LINNUM
	LDY LINNUM+1
USEDEF	STA MEMSIZ
	STY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	LDX #<RAMLOC
	LDY #>RAMLOC
	STX TXTTAB
	STY TXTTAB+1
	LDY #0
	TYA
	STA (TXTTAB)Y
	INC TXTTAB
	BNE INIT20
	INC TXTTAB+1
INIT20	LDA TXTTAB
	LDY TXTTAB+1
	JSR REASON
	JSR CRDO
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JSR SCRTCH
	JMP READY
WORDS	.BYT ' BYTES FREE'
FREMES	.BYT 19,'*** COMMODORE BASIC ***'
	.BYT 17,17,17,0
.FIL PATCH
