.PAG 'CODE4'
NEWSTT	JSR ISCNTC
	LDA TXTPTR
	LDY TXTPTR+1
	CPY #BUFPAG
	NOP
	BEQ DIRCON
	STA OLDTXT
	STY OLDTXT+1
DIRCON	LDY #0
	LDA (TXTPTR)Y
	BNE MORSTS
	LDY #2
	LDA (TXTPTR)Y
	CLC
	BNE DIRCN1
	JMP ENDCON
DIRCN1	INY
	LDA (TXTPTR)Y
	STA CURLIN
	INY
	LDA (TXTPTR)Y
	STA CURLIN+1
	TYA
	ADC TXTPTR
	STA TXTPTR
	BCC GONE
	INC TXTPTR+1
GONE	JSR CHRGET
	JSR GONE3
	JMP NEWSTT
GONE3	BEQ ISCRTS
GONE2	SBC #ENDTK
	BCC GLET
	CMP #SCRATK-ENDTK+1
	BCS SNERRX
	ASL A
	TAY
	LDA STMDSP+1,Y 
	PHA
	LDA STMDSP,Y
	PHA
	JMP CHRGET
GLET	JMP LET
MORSTS	CMP #':
	BEQ GONE
SNERR1	JMP SNERR
.SKI 3
SNERRX	CMP #GOTK-ENDTK
	BNE SNERR1
	JSR CHRGET
	LDA #TOTK
	JSR SYNCHR
	JMP GOTO
RESTOR	SEC
	LDA TXTTAB
	SBC #1
	LDY TXTTAB+1
	BCS RESFIN
	DEY
RESFIN	STA DATPTR
	STY DATPTR+1
ISCRTS	RTS
STOP	BCS STOPC
END	CLC
STOPC	BNE CONTRT
	LDA TXTPTR
	LDY TXTPTR+1
	LDX CURLIN+1
	INX
	BEQ DIRIS
	STA OLDTXT
	STY OLDTXT+1
STPEND	LDA CURLIN
	LDY CURLIN+1
	STA OLDLIN
	STY OLDLIN+1
DIRIS	PLA
	PLA
ENDCON	LDA #<BRKTXT
	LDY #>BRKTXT
	LDX #0
	STX CNTWFL
	BCC GORDY
	JMP ERRFIN
GORDY	JMP READY
CONT	BNE CONTRT
	LDX #ERRCN
	LDY OLDTXT+1
	BNE *+5
	JMP ERROR
	LDA OLDTXT
	STA TXTPTR
	STY TXTPTR+1
	LDA OLDLIN
	LDY OLDLIN+1
	STA CURLIN
	STY CURLIN+1
CONTRT	RTS
RUN	BNE *+5
	JMP RUNC
	JSR CLEARC
	JMP RUNC2
GOSUB	LDA #3
	JSR GETSTK
	LDA TXTPTR+1
	PHA
	LDA TXTPTR
	PHA
	LDA CURLIN+1
	PHA
	LDA CURLIN
	PHA
	LDA #GOSUTK
	PHA
RUNC2	JSR CHRGOT
	JSR GOTO
	JMP NEWSTT
GOTO	JSR LINGET
	JSR REMN
	LDA CURLIN+1
	CMP LINNUM+1
	BCS LUK4IT
	TYA
	SEC
	ADC TXTPTR
	LDX TXTPTR+1
	BCC LUKALL
	INX
	BCS LUKALL
LUK4IT	LDA TXTTAB
	LDX TXTTAB+1
LUKALL	JSR FNDLNC
	BCC USERR
	LDA LOWTR
	SBC #1
.FIL CODE5
