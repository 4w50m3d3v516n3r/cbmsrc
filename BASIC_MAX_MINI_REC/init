.PAG 'INIT'

	.BYTE $87,$93,$99,$99,$99,$93,$87,$FF,$99,$99,$99,$99,$99,$99,$C3,$FF
	.BYTE $99,$99,$99,$81,$99,$99,$99,$FF,$C3,$99,$9F,$9F,$9F,$99,$C3,$FF
	.BYTE $83,$99,$99,$83,$87,$93,$99,$FF,$9F,$9F,$9F,$9F,$9F,$9F,$81,$FF
	.BYTE $C3,$99,$9F,$C3,$F9,$99,$C3,$FF,$7E,$7E,$7E,$7E,$7E,$7E,$7E,$00

INIT	JSR INITCZ      ;GO INIT CHARGET & Z-PAGE
	JSR INITMS      ;GO PRINT INITILIZATION MESSAGES
	LDX #STKEND-256 ;SET UP END OF STACK
	TXS
	JMP READYX      ;JMP...READY
.SKI 4
INITAT	INC CHRGET+7
	BNE CHDGOT
	INC CHRGET+8
CHDGOT	LDA 60000
	CMP #':
	BCS CHDRTS
	CMP #' 
	BEQ INITAT
	SEC
	SBC #'0
	SEC
	SBC #$D0
CHDRTS	RTS
	.BYT 128,79,199,82,88
.SKI 6
INITCZ	LDA #76
	STA JMPER
	LDX #INITCZ-INITAT-1
MOVCHG	LDA INITAT,X
	STA CHRGET,X
	DEX
	BPL MOVCHG
	LDA #STRSIZ
	STA FOUR6
	LDA #0
	STA BITS
	STA CHANNL
	STA LASTPT+1
	LDX #1
	STX BUF-3
	STX BUF-4
	LDX #TEMPST
	STX TEMPPT
	LDX #<RAMLOC    ;BOTTOM OF MEMORY
	LDY #>RAMLOC
	STX TXTTAB      ;NOW TXTAB HAS IT
	STY TXTTAB+1
	LDX #<RAMEND    ;TOP OF MEMORY
	LDY #>RAMEND
USEDEF	STX MEMSIZ
	STY MEMSIZ+1
	STX FRETOP
	STY FRETOP+1
	LDY #0
	TYA
	STA (TXTTAB)Y
	INC TXTTAB
	BNE INIT20
	INC TXTTAB+1
INIT20	RTS
.SKI 6
INITMS	LDA TXTTAB
	LDY TXTTAB+1
	JSR REASON
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JMP SCRTCH
.END
;RSR 8/10/80 UPDATE PANIC :REM COULD USE IN ERROR ROUTINE
;RSR 2/08/82 MODIFY FOR VIC-40 RELEASE
;RSR 4/15/82 ADD ADVERTISING SIGN-ON
