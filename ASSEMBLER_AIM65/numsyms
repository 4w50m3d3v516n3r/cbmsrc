.PAGE 'NUMSYMS'

; CONVERT BASE 8,10,16 TO BINARY
; CARRY SET IF SUCCESSFUL CONVERSION, CARRY CLEAR IF ERROR.
; X MUST POINT TO CHARACTER

NUMBER	LDA #0
	STA KNVAL       ;VALUE OF NUMBER IS 0
	STA KNVAL+1
	STA KLEN
E10	JSR NUMRC       ;SEE IF NUMERIC
	BCC E20         ;NOT NUMERIC
	SBC #48         ;REMOVE ZONE
	JMP E30         ;CHECK FOR VALID BASE
E20	JSR NALPH       ;CHAR ALPHABETIC
	BCC LD94A       ;NO...MEANS ERROR
	SBC #55         ;ALPHA--REMOVE ZONE
E30	CMP KBASE       ;BASE VALID
	BCS LD94A

	INC KLEN
	PHA
	LDY KBASE+1
	BPL LD91D
	LDY #3
	LDA KNVAL+1
	ASL A
	STA TEMP+1
	LDA KNVAL
	ROL A
	STA TEMP
LD91D	ASL KNVAL+1
	ROL KNVAL
	JSR LD94F
	DEY
	BNE LD91D
	BIT KBASE+1
	BPL LD93B

E115	LDA KNVAL+1     ;ADD TO FINISH
	CLC
	ADC TEMP+1
	STA KNVAL+1
	LDA KNVAL
	ADC TEMP
	STA KNVAL

	JSR LD94F
LD93B	PLA

	CLC
	ADC KNVAL+1
	STA KNVAL+1
	BCC E130
	INC KNVAL


E130	INX
	CPX IMAXCL
	BCC E10
LD94A	LDA KLEN
	CMP #1
LD94E	RTS

LD94F	BCC LD94E
	LDA #$08
	JMP LD87B

LD956	.BYTE $02,$01,$08,$03,$0A,$FF,$10,$04

NFIND	LDA STSAVE
	STA SYMTBL
	LDA STSAVE+1
	STA SYMTBL+1
	LDA #0
	STA TOPPNT+1
	STA TOPPNT
LD96C	INC TOPPNT+1
	BNE LD972
	INC TOPPNT
LD972	LDA NOSYM+1
	CMP TOPPNT+1
	LDA NOSYM
	SBC TOPPNT
	BCC G101

XXXD2	LDY #5          ;PUT SYM INTO TABLE
G100	LDA ISYM,Y      ;SOURCE
	CMP (SYMTBL),Y    ;DESTINATION
	BNE LD994
	DEY
	BPL G100        ;NOT DONE

;
; SYMBOL IS IN TABLE- DEFINE
;

	LDY #6
	LDA (SYMTBL),Y
	STA KNVAL
	INY
	LDA (SYMTBL),Y
	STA KNVAL+1
G101	RTS

LD994	LDA SYMTBL
	CLC
	ADC #8
	STA SYMTBL
	BCC LD96C
	INC SYMTBL+1
	JMP LD96C

NSERT	STA KNVAL
	STY KNVAL+1
	LDA SYMTBL
	CMP ISYEND
	LDA SYMTBL+1
	SBC ISYEND+1
	BCS LD9CC
	LDY #5
LD9B2	LDA ISYM,Y
	STA (SYMTBL),Y
	DEY
	BPL LD9B2
	LDY #$06
	LDA KNVAL
	STA (SYMTBL),Y
	INY
	LDA KNVAL+1
	STA (SYMTBL),Y
	INC NOSYM+1
	BNE LD9CB
	INC NOSYM
LD9CB	RTS

LD9CC	LDX #MSG2-MSGS
	JSR LDFE1
	JMP LD000


; SEARCH OPCODE TAB FOR OPCODE
; CARRY SET IF OPCODE FOUND, CARRY CLEAR IF OPCODE NOT FOUND.

NOPFND
	LDA #<OPRNDS
	LDY #>OPRNDS
	LDX #NUMASM-1
	JSR LD9EA
	BCC LD9E9

	LDA KTMPL,X     ;TEMPLATE
	STA JOPTEM
	LDA KCODE,X     ;BASE OPCODE
	STA JOPBAS
LD9E9	RTS


LD9EA	STA TBLPTR
	STY TBLPTR+1
LD9EE	LDY #2
LD9F0	LDA ISYM,Y
	CMP (TBLPTR),Y
	BNE LD9FC
	DEY
	BPL LD9F0
	SEC
	RTS

;
; NO MATCH
;

LD9FC	LDA TBLPTR
	SEC
	SBC #3
	STA TBLPTR
	BCS LDA07
	DEC TBLPTR+1
LDA07	DEX
	BPL LD9EE
	CLC             ;OPCODE NOT FOUND
LDA0B	RTS

.END
