;*****CODE3*****
LIST	BCC GOLST
	BEQ GOLST
	CMP #MINUTK
	BNE STKRTS
GOLST	JSR LINGET
	JSR FNDLIN
	JSR CHRGOT
	BEQ LSTEND
	CMP #MINUTK
	BNE FLNRTS
	JSR CHRGET
	JSR LINGET
	BNE FLNRTS
LSTEND	PLA
	PLA
	LDA LINNUM
	ORA LINNUM+1
	BNE LIST4
	LDA #255
	STA LINNUM
	STA LINNUM+1
LIST4	LDY #1
	STY DORES
	LDA (LOWTR)Y
	BEQ GRODY
	JSR ISCNTC
	JSR CRDO
	INY
	LDA (LOWTR)Y
	TAX
	INY
	LDA (LOWTR)Y
	CMP LINNUM+1
	BNE TSTDUN
	CPX LINNUM
	BEQ TYPLIN
TSTDUN	BCS GRODY
TYPLIN	STY LSTPNT
	JSR LINPRT
	LDA #' 
PRIT4	LDY LSTPNT
	AND #127
PLOOP	JSR OUTDO
	CMP #34
	BNE PLOOP1
	LDA DORES
	EOR #@377
	STA DORES
PLOOP1	INY
	BEQ GRODY
	LDA (LOWTR)Y
	BNE QPLOP
	TAY
	LDA (LOWTR)Y
	TAX
	INY
	LDA (LOWTR)Y
	STX LOWTR
	STA LOWTR+1
	BNE LIST4
GRODY	JMP READY
QPLOP	BPL PLOOP
	CMP #PI
	BEQ PLOOP
	BIT DORES
	BMI PLOOP
	TAX
	STY LSTPNT
.SKI 3
	LDY #>RESLST
	STY INDEX1+1
	LDY #<RESLST
	STY INDEX1
	LDY #0
	ASL A
	BEQ PRIT3B
.SKI 3
;POINT INDEX1 AT START OF WORD
RESRCH	DEX
	BPL PRIT3
RESCR1	INC INDEX1
	BNE RESCR2
	INC INDEX1+1
RESCR2	LDA (INDEX1)Y
	BPL RESCR1
	BMI RESRCH
.SKI 3
;LIST WORD INDEXING BY Y
PRIT3	INY
PRIT3B	LDA (INDEX1)Y
	BMI PRIT4
	JSR OUTDO
	BNE PRIT3
.SKI 3
FOR	LDA #128
	STA SUBFLG
	JSR LET
	JSR FNDFOR
	BNE NOTOL
	TXA
	ADC #FORSIZ-3
	TAX
	TXS
NOTOL	PLA
	PLA
	LDA #8+ADDPRC
	JSR GETSTK
	JSR DATAN
	CLC
	TYA
	ADC TXTPTR
	PHA
	LDA TXTPTR+1
	ADC #0
	PHA
	LDA CURLIN+1
	PHA
	LDA CURLIN
	PHA
	LDA #TOTK
	JSR SYNCHR
	JSR CHKNUM
	JSR FRMNUM
	LDA FACSGN
	ORA #127
	AND FACHO
	STA FACHO
	LDA #<LDFONE
	LDY #>LDFONE
	STA INDEX1
	STY INDEX1+1
	JMP FORPSH
LDFONE	LDA #<FONE
	LDY #>FONE
	JSR MOVFM
	JSR CHRGOT
	CMP #STEPTK
	BNE ONEON
	JSR CHRGET
	JSR FRMNUM
ONEON	JSR SIGN
	JSR PUSHF
	LDA FORPNT+1
	PHA
	LDA FORPNT
	PHA
	LDA #FORTK
	PHA
.FIL CODE4
