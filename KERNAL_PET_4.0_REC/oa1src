.PAG 'SCREEN EDITOR & I/O'
	*=$6000+OFFSET  ;OFFSET IS FOR A
;*** SOFT MACHINE ******
CINT	LDA #$7F
	STA IER
	LDX #CAS2-TIME
	LDA #0
PX1	STA TIME,X
	DEX
	BPL PX1
	LDA #<KEY
	STA CINV
	LDA #>KEY
	STA CINV+1
	LDA #3
	STA DFLTO
	LDA #$F
	STA PIAL
	ASL A
	STA PIA
	STA P2DB
	STX IEEO
	STX TIH
	LDA #$3D
	STA PIAS
	BIT PIAK
	LDA #$3C
	STA IEEIS
	STA IEEOS
	STA PIAL1
	STX IEEO
	LDA #$C
	STA PCR
	STA BLNCT
	STA BLNSW
CLSR	LDY #$83
	LDX #24
LPS1	STY LDTB1,X
	CPX #20
	BEQ LPS2
	CPX #13
	BEQ LPS2
	CPX #7
	BNE LPS3
LPS2	DEY
LPS3	DEX
	BPL LPS1
	STY POINT
	INX
	STX RVS
	STX POINT-1
	LDA #' 
LPS4	STA $8000,X
	STA $8100,X
	STA $8200,X
	STA $8300,X
	DEX
	BNE LPS4
NXTD	LDY #0
	STY PNTR
	STY TBLX
STUPT	LDX TBLX
	LDA LDTB1,X
	ORA #$80
	STA POINT
	LDA LDTB2,X
	STA PNT
	LDA #39
	STA LNMX
	CPX #24
	BEQ STUPZ
	LDA LDTB1+1,X
	BMI STUPZ
	LDA #79
	STA LNMX
STUPZ	LDA PNTR
	CMP #40
	BCC STUPR
	SBC #40
	STA PNTR
STUPR	RTS
LP2	LDY KEYD
	LDX #0
LP1	LDA KEYD+1,X
	STA KEYD,X
	INX
	CPX NDX
	BNE LP1
	DEC NDX
	TYA
	CLI
	RTS
LOOP4	JSR PRT
LOOP3	LDA NDX
	STA BLNSW
	BEQ LOOP3
	SEI
	LDA BLNON
	BEQ LP21
	LDA GDBLN
	LDY #0
	STY BLNON
	JSR DSPP
LP21	JSR LP2
	CMP #$83
	BNE LP22
	SEI
	LDX #9
	STX NDX
LP23	LDA RUNTB-1,X
	STA KEYD-1,X
	DEX
	BNE LP23
	BEQ LOOP3
LP22	CMP #$D
	BNE LOOP4
	LDY LNMX
	STY CRSW
CLP5	LDA (PNT)Y
	CMP #' 
	BNE CLP6
	DEY
	BNE CLP5
CLP6	INY
	STY INDX
	LDY #0
	STY PNTR
	STY QTSW
	LDA LSXP
	BMI LOP5
	CMP TBLX
	BNE LOP5
	LDA LSTP
	STA PNTR
	CMP INDX
	BCC LOP5
	BCS CLP2
LOOP5	TYA
	PHA
	TXA
	PHA
	LDA CRSW
	BEQ LOOP3
LOP5	LDY PNTR
	LDA (PNT)Y
	STA DATA
LOP51	AND #$3F
	ASL DATA
	BIT DATA
	BPL LOP54
	ORA #$80
LOP54	BCC LOP52
	LDX QTSW
	BNE LOP53
LOP52	BVS LOP53
	ORA #$40
LOP53	INC PNTR
	JSR QTSWC
	CPY INDX
	BNE CLP1
CLP2	LDA #0
	STA CRSW
	LDA #$D
	LDX DFLTN       ;FIX GETS FROM SCREEN
	CPX #3          ;1S IT THE SCREEN?
	BEQ CLP2A
	LDX DFLTO
	CPX #3
	BEQ CLP21
CLP2A	JSR PRT
CLP21	LDA #$D
CLP1	STA DATA
	PLA
	TAX
	PLA
	TAY
	LDA DATA
	CMP #$DE
	BNE CLP7
	LDA #$FF
CLP7	RTS
QTSWC	CMP #$22
	BNE QTSWL
	LDA QTSW
	EOR #$1
	STA QTSW
	LDA #$22
QTSWL	RTS
NXT33	ORA #$40
NXT3	LDX RVS
	BEQ NVS
NC3	ORA #$80
NVS	LDX INSRT
	BEQ NVS1
	DEC INSRT
NVS1	JSR DSPP
JSTS	INC PNTR
	LDY LNMX
	CPY PNTR
	BCS LOOP2
	LDX TBLX
	CPY #79
	BNE JSTS1
	JSR JSTSX
	JSR NXLN
	LDA #0
	STA PNTR
	BEQ LOOP2
JSTS1	CPX #24
	BNE JTS2
	JSR SCRL
LOOP2	PLA
	TAY
	LDA INSRT
	BEQ LOP2
	LSR QTSW
LOP2	PLA
	TAX
	PLA
	CLI
	RTS
JSTSX	CPX #23
	BCS JSXB
	LDA LDTB1+2,X
	ORA #$80
	STA LDTB1+2,X
JSXB	RTS
JTS2	JSR JSTS2
	JMP LOOP2
SCRL	JSR SCROL
	DEC LSXP        ;FIX INPUT SCROLL
	DEC TBLX
	LDX TBLX
JSTS2	ASL LDTB1+1,X
	LSR LDTB1+1,X
	JSR JSTSX
	LDA PNTR
	PHA
	JSR STUPT
	PLA
	STA PNTR
	RTS
BKLN	LDY #39
	LDX TBLX
	BNE BKLN1
	STX PNTR
	PLA
	PLA
	BNE LOOP2
BKLN1	LDA LDTB1-1,X
	BMI NTCN2
	DEX
	LDA LDTB1-1,X
	LDY #79
NTCN2	DEX
	STX TBLX
	STA POINT
	LDA LDTB2,X
	STA PNT
	STY PNTR
	STY LNMX
	RTS
PRT	PHA
	STA DATA
	TXA
	PHA
	TYA
	PHA
	LDA #0
	STA CRSW
	LDY PNTR
	LDA DATA
	BPL *+5
	JMP NXTX
	CMP #$D
	BNE NJT1
	JMP NXT1
NJT1	CMP #' 
	BCC NTCN
	AND #$3F
	JSR QTSWC
	JMP NXT3
NTCN	LDX INSRT
	BEQ CNC3X
	JMP NC3
CNC3X	CMP #$14
	BNE NTCN1
	DEY
	STY PNTR
	BPL BK1
	JSR BKLN
	JMP BK2
BK1	INY
	LDA (PNT)Y
	DEY
	STA (PNT)Y
	INY
	CPY LNMX
	BNE BK1
BK2	LDA #' 
	STA (PNT)Y
	BNE JPL3
NTCN1	LDX QTSW
	BEQ NC3W
CNC3	JMP NC3
NC3W	CMP #$12
	BNE NC1
	STA RVS
NC1	CMP #$13
	BNE NC2
	JSR NXTD
NC2	CMP #$1D
	BNE NCX2
	INY
	STY PNTR
	DEY
	CPY LNMX
	BCC NCZ2
	JSR NXLN
	LDY #0
JPL4	STY PNTR
NCZ2	JMP LOOP2
NCX2	CMP #$11
	BNE JPL3
	CLC
	TYA
	ADC #40
	TAY
	CMP LNMX
	BCC JPL4
	BEQ JPL4
	JSR NXLN
JPL3	JMP LOOP2
PRT1	INX
	STA TBLX
	TYA
	SBC #40
	STA PNTR
PRT2	INC TBLX
	LDA LDTB2
	STA PNT
	LDA LDTB1
	STA POINT
	BNE JPL3
NXTX	AND #$7F
	CMP #$7F
	BNE NXTX1
	LDA #$5E
NXTX1	CMP #' 
	BCC *+5
	JMP NXT33
	CMP #$D
	BNE UP5
	JMP NXT1
UP5	LDX  QTSW
	BNE UP6
	CMP #$14
	BNE UP9
	LDY LNMX
	LDA (PNT)Y
	CMP #' 
	BNE INS3
	CPY PNTR
	BNE INS1
INS3	CPY #79
	BEQ JPL3
	JSR NEWLN
INS1	LDY LNMX
INS2	DEY
	LDA (PNT)Y
	INY
	STA (PNT)Y
	DEY
	CPY PNTR
	BNE INS2
	LDA #$20
	STA (PNT)Y
	INC INSRT
	BNE JLP2
UP9	LDX INSRT
	BEQ UP2
UP6	ORA #$40
	JMP NC3
UP2	CMP #$11
	BNE NXT2
	LDA PNTR
	CMP #40
	BCC UP1
	SBC #40
	STA PNTR
	BCS JLP2
UP1	LDX TBLX
	BEQ JLP2
	LDA LDTB1-1,X
	BPL UP3
	DEC TBLX
	JSR STUPT
	BCC JLP2
UP3	DEX
	DEX
	STX TBLX
	JSR STUPT
	LDA PNTR
	CLC
	ADC #40
	STA PNTR
	BNE JLP2
NXT2	CMP #$12
	BNE NXT6
	LDA #0
	STA RVS
NXT6	CMP #$1D
	BNE NXT61
	DEY
	STY PNTR
	BPL JLP2
	JSR BKLN
	JMP LOOP2
NXT61	CMP #$13
	BNE JLP2
	JSR CLSR
JLP2	JMP LOOP2
NXLN	SEC
	LSR LSXP
	LDX TBLX
NXLN2	INX
	CPX #25
	BNE NXLN1
	JSR SCROL
NXLN1	LDA LDTB1,X
	BPL NXLN2
	STX TBLX
	JMP STUPT
NXT1	LDA #0
	STA INSRT
	STA RVS
	STA QTSW
	STA PNTR
	JSR NXLN
	JMP LOOP2
.FILE OA2SRC
