.PAG 'DOS SYNTAX'
;*******************************
;*   COMMODORE DOS PARSER      *
;*                             *
;*     -MGM  7/11/79-          *
;*                             *
;* THIS SET OF ROUTINES TAKES  *
;* TOKENS AND VALUES AFTER THE *
;* THE FOLLOWING BASIC KEYHORDS*
;*  DOPEN; DCLOSE ; RECORD ;   *
;*  FORMAT ; COLLECT ; BACKUP ;*
;*  COPY   ; CONCAT  ; DSAVE  ;*
;* DLOAD  ; CATLOG ; RENAME ;  *
;* APPEND ; SCRTCH             *
;*******************************
;
;IT THEN PARSES THE FOLLOWING LINE
; AND FINDS SYNTAX ERRORS, CHECKS
;FOR OUT OF RANGE VALUES, AND SETS
; VARIABLES IN THE ZERO-PAGE TO BE
; PASSED TO THE DISK MESSAGE GEN-
; ERATOR (DMG)
;
;
SYNERR	JMP SNERR       ;SYNTAX ERROR
;
;************************************
;* "RECORD" PARSER & SYNTAX CHECKER *
;*   NOTE: POSITION DEPENDENT       *
;************************************
RECORD	LDA #01         ;FIX POS
	STA POS
	JSR CHRGOT
	LDA #'#
	JSR SYNCHR      ;NXT CHR MUST BE '#'
	JSR GTVL2       ;GET NXT VALUE
	CPX #0
	BEQ QTYER1      ;ADDR CAN'T BE < 1
	STX LA          ;STORE LOGICAL ADDR
	JSR CHKCOM      ;NXT MUST BE COMMA
	BEQ SYNERR      ;<CR>/<:>  NOT ALLOWED
	BCC NUMADR      ;MUST BE NUM OR "("
	JSR CHKOPN      ;MUST BE "("
	JSR FRMEVL
	JSR GETADR
	JSR CHKCLS
	JMP REXNEX
NUMADR	JSR FRMEVL
	JSR GETADR      ;SEE WHAT NXT IS
REXNEX	JSR CHRGOT       ;SEE WHAT NXT IS
	BEQ DONER       ;<CR>/<:> => DONE
	JSR CHKCOM      ;ELSE MUST BE COMMA
	BEQ SYNERR      ; CANT BE <CR> OR <:>
	JSR GTVL2
	CPX #0
	BEQ QTYER1      ; OUT OF RANGE
	CPX #$FF
	BEQ QTYER1      ;OUT OF RANGE
	STX POS         ;STORE POSITION OF REC
	JSR CHRGOT
	BNE SYNERR      ;MUST BE <CR> OR <:>
DONER	JMP BOBREC
;
.PAG 'DOS SYNTAX'


QTYER1	JMP QTYERR
;
;**********************************
;*          SYNTAX CHECKER        *
;* ROUTINES FOR DOS WRITE         *
;**********************************
CHK1	AND #$E6;FOR HEADER,DLDAD,SCRTCH
	BEQ CHK2        ;CHK OPT PARMS
CHKER1	JMP SNERR
CHK2	LDA PARCHK      ;FOR DSAVE
	AND #1
	CMP #1          ;CHK REQ'D PARMS
	BNE CHKER1      ;ERROR IF 1 MISSING
	LDA PARCHK      ;RELOAD FOR RETURN
	RTS
;
CHK3	AND #$E7        ;FOR COLECT,CATLOG
	BNE CHKER1      ;CHK OPT PARMS
	RTS
;
CHK4	AND #$C4        ;FOR COPY,CONCAT
	BNE CHKER1      ;CHK OPT PARMS
	LDA PARCHK
CHK5	AND #3          ;FOR RENAME
	CMP #3          ;CHK REQ'D PARMS
	BNE CHKER1
	LDA PARCHK      ;RELOAD FOR RETURN
	RTS
;
CHK6	AND #5          ;FOR APPEND,DOPEN
	CMP #5          ;CHK REQ'D PARNS
	BNE CHKER1
	LDA PARCHK      ;RELOAD FOR RTS
	RTS
;
.FILE DOS.WRITE
