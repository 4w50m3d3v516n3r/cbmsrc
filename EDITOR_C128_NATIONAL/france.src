	.nam  C/128 FRANCE/BELGIUM NATIONAL EDITOR
	.subttl *** COPYRIGHT (C) 1987  by  COMMODORE BUSINESS MACHINES, INC. ***
	.forml 60

country	= $02		;FRANCE/BELGIUM		p/n 325166-03  04/08/87  F.Bowen
checksum= $11		;character ROM checksum (same as ITALY)
chr_sig	= $03dc		;character ROM signature
rom_sig	= $4130		;ROM signature


;  ******************************************************************
;  *                        //                                      *
;  *        CCCCCCC        //    111     2222222     8888888        *
;  *       CCC   CCC      //    1111    222   222   888   888       *
;  *      CCC            //      111         222    888   888       *
;  *      CCC           //       111       222        88888         *
;  *      CCC          //        111     222        888   888       *
;  *       CCC   CCC  //         111    222   222   888   888       *
;  *        CCCCCCC  //        1111111  222222222    8888888        *
;  *                //                                              * 
;  *                                                                * 
;  * EEEEEEEEE  DDDDDDDD    IIIII TTTTTTTTT   OOOOOOO    RRRRRRRR   * 
;  * EEE        DDD   DDD    III     TTT     OOO   OOO   RRR   RRR  * 
;  * EEE        DDD    DDD   III     TTT    OOO     OOO  RRR   RRR  * 
;  * EEEEEE     DDD    DDD   III     TTT    OOO     OOO  RRRRRRRR   * 
;  * EEE        DDD    DDD   III     TTT    OOO     OOO  RRR RRR    * 
;  * EEE        DDD   DDD    III     TTT     OOO   OOO   RRR  RRR   * 
;  * EEEEEEEEE  DDDDDDDD    IIIII    TTT      OOOOOOO    RRR   RRR  * 
;  *                                                                * 
;  *                                                                * 
;  *    COPYRIGHT (C)1987  BY   COMMODORE BUSINESS MACHINES, INC.   * 
;  *                                                                * 
;  ****************************************************************** 
  
  
  
;  ****************************************************************** 
;  *                                                                * 
;  * THIS LISTING CONTAINS CONFIDENTIAL AND PROPRIETARY INFORMATION *
;  * OF CBM, INC.   REPRODUCTION,  DISSEMINATION  OR  DISCLOSURE TO *
;  * OTHERS WITHOUT EXPRESS WRITTEN PERMISSION IS PROHIBITED.  THIS *
;  * SOFTWARE IS INTENDED FOR USE IN  COMMODORE C/128 SYSTEMS ONLY. *
;  *                                                                * 
;  *  THE  INFORMATION  IN  THIS  DOCUMENT  IS  SUBJECT  TO CHANGE  * 
;  *                       WITHOUT NOTICE.                          * 
;  *                                                                * 
;  *  NO  RESPONSIBILITY  IS  ASSUMED  FOR THE RELIABILITY OF THIS  * 
;  *                          SOFTWARE.                             * 
;  *                                                                * 
;  ****************************************************************** 
  
	.page










;  ****************************************************************** 
;  *                                                                * 
;  * THIS VERSION OF THE COMMODORE SCREEN EDITOR CONTAINS  SPECIFIC *
;  * MODIFICATIONS FOR THE IMPLEMENTATION OF THE FOLLOWING NATIONAL *
;  * KEYBOARD AND SCREEN EDITOR:                                    * 
;  *								    *
;  *         *****   ****      *     *   *    ***    *****          *
;  *         *       *   *    * *    **  *   *   *   *              *
;  *         ****    ****    *****   * * *   *       ****           *
;  *         *       *  *    *   *   *  **   *   *   *              *
;  *         *       *   *   *   *   *   *    ***    *****          *
;  *                                                                *
;  *       ****    *****   *       ***   ***   *   *   *    *       *
;  *       *   *   *       *      *       *    *   *   **  **       *
;  *       ****    ****    *      * ***   *    *   *   * ** *       *
;  *       *   *   *       *      *   *   *    *   *   *    *       *
;  *       ****    *****   *****   ***   ***    ***    *    *       *
;  *                                                                *
;  *                                                                * 
;  *		 THE ORIGINAL VERSION HEREIN IS BY:                 *
;  *	    DR. KARL BENDER, WALTER SCHMITZ, and FRED BOWEN	    *
;  *                                                                * 
;  ****************************************************************** 



;.end
	.include sysdoc
	.include relnotes
	.include declare
	.include entries

	* = $c000

.editor_check_begin

	.include ed1		;contains NATIONAL patches (4 places)
	.include ed2
	.include ed3		;contains NATIONAL patches (1 place)
	.include ed4
	.include ed5
	.include ed6
	.include routines
	.include ed7		;contains an ORG $FA80
	.include patches	;contains an ORG $CF00 (& upper limit @ $CF7F)

;	*****   ROM SPACE FROM  $FC80 THRU $FEFF IS RESERVED FOR   *****
;	*****   NATIONAL KEYBOARD DRIVERS AND ASSOCIATED TABLES.   *****

; 	.include <nation>.src	;contains an ORG $FC80 for national driver & tables

;	.end
	.page
	.subttl  C/128 FRANCE/BELGIUM KEYBOARD DRIVER (01/15/86)

	* = $CFF8	;reserved ROM signature area

	.byte checksum
	.byte country
	.word chr_sig
	.word rom_sig


	* = $FC80	;reserved ROM area

nat_init
	sta dk_flag	;'cint' patch (.a=0)
	sta sidreg+24
	rts


nat_scnkey
	bit dk_flag
	bmi do_scan	;...branch if national keys disabled

	lda shflag	;test 'CAPS LOCK' for keyboard selection
	and #$10
	beq 10$		;...branch if up- use ASCII
	lda decode+1
	cmp #>nat_mode1
	beq do_scan	;...branch if national keys already setup
	lda #<nat_decode
	ldy #>nat_decode
	bne switch	;...else setup national keys

10$	lda decode+1
	cmp #>mode1
	beq do_scan	;...branch if ascii keys already setup
	lda #<keycod	;...else setup ascii keys
	ldy #>keycod

switch	sta keytab
	sty keytab+1
	ldy #11		;point keyscan to decode tables
10$	lda (keytab),y
	sta decode,y
	dey
	bpl 10$

	iny
	sty dk_flag	;clear any pending dead keys
	php
	sei
	jsr init80	;move new character set to 8563 RAM
	plp
	rts

do_scan
	jmp scnkey	;resume normal keyscan

	.page
nat_keychk
	ldx decode+1
	cpx #>nat_mode1
	bne 70$		;...branch to normal keyscan if ASCII mode
	ldx dk_flag
	bmi 70$		;...branch to normal keyscan if national scan disabled
	beq 25$		;...branch if previous key was not a dead key

	ldy deadp,x	;previous key was a dead key- index in .x
	dex
10$	dey
	pha		;save .a (current character)
	tya
	cmp deadp,x	;look to see if index (.y) is out of range
	pla		;restore current character
	bcc 20$		;...branch if not in table (out of range)
	cmp oldead-1,y	;look to see if current key can be substituted
	bne 10$		;...branch if not

	lda nwdead-1,y	;substitute it
20$	pha
	and #$7f	;weed out control codes
	cmp #$20
	pla
	bcc 45$		;...branch if control code & ignore it

25$	ldx #5
30$	cmp dktbl-1,x	;look to see if current key is a dead key
	beq 40$		;...branch if it is
	dex
	bne 30$

40$	stx dk_flag	;save for next time...
	cpx #$00
	beq 70$		;if current key is not a dead key we are done

	tay		;save dead key code
	bit insflg	;cannot display dead key if auto-insert mode active
	bmi 45$		;...branch if active
	bit mode	;which screen (40/80) is active?
	bpl 50$		;...branch if 40

	ldx #10
	jsr vdcin	;read 8563 register 10 to determine if cursor is on
	and #$40
	bne 60$		;...branch if cursor is on
45$	rts		;...else return & buffer nothing (dead key flag is set)

50$	lda blnsw
	bne 45$		;...branch if cursor is off

60$	tya		;display dead key using screen code
	ora #$40
	and #$7f	;convert dead key character to screen code
	jmp displc	;put character on screen at cursor without moving cursor
			;and without buffering dead key (dead key flag is set)


70$	ldx shflag	;setup registers as normal
	ldy lstx
	jmp (keychk)	;resume normal keyscan thru indirect

	.page
	.subttl  FRANCE/BELGIUM NATIONAL KEYBOARD TABLES

nat_mode1		;unshifted keys
	.byte $14,$0d,$1d,$88,$85,$86,$87,$11,$22,$5a,$51,$27,$57,$53,$45,$01
	.byte $28,$52,$44,$b0,$43,$46,$54,$58,$b2,$59,$47,$21,$42,$48,$55,$56
	.byte $df,$49,$4a,$b1,$2c,$4b,$4f,$4e,$29,$50,$4c,$2d,$3a,$4d,$ae,$3b
	.byte $40,$24,$b5,$13,$01,$5e,$2a,$3d,$26,$3c,$04,$bf,$20,$02,$41,$03
	.byte $84,$38,$35,$09,$32,$34,$37,$31,$1b,$2b,$2d,$0a,$0d,$36,$39,$33
	.byte $08,$30,$2e,$91,$11,$9d,$1d,$ff,$ff

nat_mode2		;shifted keys
	.byte $94,$8d,$9d,$8c,$89,$8a,$8b
	.byte $91,$33,$da,$d1,$34,$d7,$d3,$c5,$01,$35,$d2,$c4,$36,$c3,$c6,$d4
	.byte $d8,$37,$d9,$c7,$38,$c2,$c8,$d5,$d6,$39,$c9,$ca,$30,$3f,$cb,$cf
	.byte $ce,$dd,$d0,$cc,$5f,$2f,$cd,$af,$2e,$23,$5b,$25,$93,$01,$5c,$5d
	.byte $2b,$31,$3e,$04,$32,$a0,$02,$c1,$83,$84,$a9,$ab,$18,$a3,$a5,$a8
	.byte $a2,$1b,$a1,$2d,$0a,$8d,$a7,$aa,$a4,$08,$a6,$de,$91,$11,$9d,$1d
	.byte $ff,$ff
nat_mode3		;C= keys
	.byte $94,$8d,$9d,$8c,$89,$8a,$8b,$91,$96,$da,$d1,$97,$d7,$d3
	.byte $c5,$01,$98,$d2,$c4,$99,$c3,$c6,$d4,$d8,$9a,$d9,$c7,$9b,$c2,$c8
	.byte $d5,$d6,$29,$c9,$ca,$30,$3f,$cb,$cf,$ce,$dd,$d0,$cc,$5f,$2f,$cd
	.byte $af,$2e,$23,$5b,$25,$93,$01,$5c,$5d,$2b,$81,$3c,$04,$95,$a0,$02
	.byte $c1,$03,$84,$a9,$ab,$18,$a3,$a5,$a8,$a2,$1b,$a1,$2d,$0a,$8d,$a7
	.byte $aa,$a4,$08,$a6,$de,$91,$11,$9d,$1d,$ff,$ff

nat_decode
	.word nat_mode1, nat_mode2, nat_mode3, mode4
	.word nat_mode1, nat_mode1

	.page
dktbl	.byte $ad,$c0,$ae,$af,$00		;dead keys

deadp	.byte $01,$02,$07,$0c,$11,$11		;pointers to 'build' keys

oldead	.byte $45,$41,$45,$49,$4f,$55,$41,$45	;new key tables
	.byte $49,$4f,$55,$41,$45,$49,$4f,$55
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;unused
	.byte $ff,$ff,$ff

nwdead	.byte $bf,$b1,$b2,$b3,$b4,$b5,$b6,$b7	;substitute key tables
	.byte $b8,$b9,$ba,$bb,$db,$dc,$bc,$bd
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;unused
	.byte $ff,$ff,$ff

	.page

; NATIONAL patch code.	The following area contains patches specifically
;			related to national keyboard implementations.
;
;   (patch first included in part #315078-02,  10/01/85   F.Bowen)

ascii_din
	lda r6510	;scan for ASCII/DIN key (a.k.a. CAPS LOCK)
	and #$40	;P6 is it
	eor #$40	;invert sense.....up=0, dwn=1
	lsr a
	lsr a
	sta shflag	;end of special abrieviated keyscan
	rts


.editor_check_top

	.subttl FRANCE/BELGIUM EDITOR CROSS REFERENCE
	.end
