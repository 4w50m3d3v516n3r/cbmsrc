.PAG 'CODE7'
STROUT	JSR STRLIT
STRPRT	JSR FREFAC
	TAX
	LDY #0
	INX
STRPR2	DEX
	BEQ PRTRTS
	LDA (INDEX)Y
	JSR OUTDO
	INY
	CMP #13
	BNE STRPR2
	JSR CRFIN
	JMP STRPR2
CRTSKP	LDA #29
	.BYT $2C
OUTQST	LDA #'?
OUTDO	BIT CNTWFL
	BMI OUTRTS
	PHA
	CMP #29
	BEQ OUTDO1
	CMP #$9D
	BEQ LCA5A
	CMP #20
	BNE LCA64
LCA5A	LDA TRMPOS
	BEQ TRYOUT
	LDA CHANNL
	BNE TRYOUT
	DEC TRMPOS
LCA64	AND #127
	CMP #' 
	BCC TRYOUT
OUTDO1	LDA CHANNL
;       BNE TRYOUT
;       INC TRMPOS
	JSR PATCH6
	NOP
TRYOUT	PLA
	JSR OUTCH
OUTRTS	AND #255
	RTS
TRMNOK	LDA INPFLG
	BEQ TRMNO1
GETDTL
;       LDA DATLIN
;       LDY DATLIN+1
	JSR PATCH5
	NOP
STCURL	STA CURLIN
	STY CURLIN+1
SNERR4	JMP SNERR
TRMNO1	LDA CHANNL
	BEQ DOAGIN
	LDX #ERRBD
	JMP ERROR
DOAGIN	LDA #<TRYAGN
	LDY #>TRYAGN
	JSR STROUT
	LDA OLDTXT
	LDY OLDTXT+1
	STA TXTPTR
	STY TXTPTR+1
	RTS
GET	JSR ERRDIR
	CMP #'#
	BNE GETTTY
	JSR CHRGET
	JSR GETBYT
	LDA #44
	JSR SYNCHR
	JSR COIN
	STX CHANNL
ZZ2=BUF+1
GETTTY	LDX #<ZZ2
	LDY #0
	STY BUF+1
	LDA #64
	JSR INPCO1
	LDX CHANNL
	BNE IORELE
	RTS
INPUTN	JSR GETBYT
	LDA #44
	JSR SYNCHR
	JSR COIN
	STX CHANNL
	JSR NOTQTI
IODONE	LDA CHANNL
IORELE	JSR CCCHN
	LDX #0
	STX CHANNL
	RTS
INPUT	LSR CNTWFL
	CMP #34
	BNE NOTQTI
	JSR STRTXT
	LDA #59
	JSR SYNCHR
	JSR STRPRT
NOTQTI	JSR ERRDIR
	LDA #44
	STA BUF-1
GETAGN	JSR QINLIN
	LDA CHANNL
	BEQ BUFFUL
	LDA CSTAT
	AND #2
	BEQ BUFFUL
	JSR IODONE
	JMP DATA
BUFFUL	LDA BUF
	BNE INPCON
	LDA CHANNL
	BNE GETAGN
;       JMP STPEND
	JMP PATCH1
QINLIN	LDA CHANNL
	BNE GINLIN
	JSR OUTQST
	JSR CRTSKP
GINLIN	JMP INLIN
READ	LDX DATPTR
	LDY DATPTR+1
	.BYT $A9
INPCON	TYA
INPCO1	STA INPFLG
	STX INPPTR
	STY INPPTR+1
INLOOP	JSR PTRGET
	STA FORPNT
	STY FORPNT+1
	LDA TXTPTR
	LDY TXTPTR+1
	STA VARTXT
	STY VARTXT+1
	LDX INPPTR
	LDY INPPTR+1
	STX TXTPTR
	STY TXTPTR+1
	JSR CHRGOT
	BNE DATBK1
	BIT INPFLG
	BVC QDATA
	JSR CGETL
	STA BUF
ZZ4=BUF-1
	LDY #>ZZ4
	LDX #<ZZ4
	BNE DATBK
QDATA	BMI DATLOP
	LDA CHANNL
	BNE GETNTH
	JSR OUTQST
GETNTH	JSR QINLIN
DATBK	STX TXTPTR
	STY TXTPTR+1
.FIL CODE8
