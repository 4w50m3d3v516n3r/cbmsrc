.PAG 'CODE6'
GETSPT	LDY #2
	LDA (FACMO)Y
	CMP FRETOP+1
	BCC DNTCPY
	BNE QVARIA
	DEY
	LDA (FACMO)Y
	CMP FRETOP
	BCC DNTCPY
QVARIA	LDY FACLO
	CPY VARTAB+1
	BCC DNTCPY
	BNE COPY
	LDA FACMO
	CMP VARTAB
	BCS COPY
DNTCPY	LDA FACMO
	LDY FACMO+1
	JMP COPYC
COPY	LDY #0
	LDA (FACMO)Y
	JSR STRINI
	LDA DSCPNT
	LDY DSCPNT+1
	STA STRNG1
	STY STRNG1+1
	JSR MOVINS
	LDA #<DSCTMP
	LDY #>DSCTMP
COPYC	STA DSCPNT
	STY DSCPNT+1
	JSR FRETMS
	LDY #0
	LDA (DSCPNT)Y
	STA (FORPNT)Y
	INY
	LDA (DSCPNT)Y
	STA (FORPNT)Y
	INY
	LDA (DSCPNT)Y
	STA (FORPNT)Y
	RTS
STRDON	JSR STRPRT
NEWCHR	JSR CHRGOT
PRINT	BEQ CRDO
PRINTC	BEQ PRTRTS
	CMP #TABTK
	BEQ TABER
	CMP #SPCTK
	CLC
	BEQ TABER
	CMP #44
	BEQ COMPRT
	CMP #59
	BEQ NOTABR
	JSR FRMEVL
	BIT VALTYP
	BMI STRDON
	JSR FOUT
	JSR STRLIT
	JSR STRPRT
	JSR OUTSPC
	BNE NEWCHR
FININL	LDA #0
	STA BUF,X
ZZ5=BUF-1
	LDX #<ZZ5
	LDY #>ZZ5
	LDA CHANNL
	BNE PRTRTS
CRDO	LDA #13
	JSR OUTDO
	BIT CHANNL
	BPL CRFIN
;
	LDA #10
	JSR OUTDO
CRFIN	EOR #255
PRTRTS	RTS
COMPRT	LDA PNTR
NCMPOS	=@36
	SEC
MORCO1	SBC #CLMWID
	BCS MORCO1
	EOR #255
	ADC #1
	BNE ASPAC
TABER	PHP
	LDX PNTR
	STX TRMPOS
	JSR GTBYTC
	CMP #41
	BNE SNERR4
	PLP
	BCC XSPAC
	TXA
	SBC TRMPOS
	BCC NOTABR
ASPAC	TAX
XSPAC	INX
XSPAC2	DEX
	BNE XSPAC1
NOTABR	JSR CHRGET
	JMP PRINTC
.END
