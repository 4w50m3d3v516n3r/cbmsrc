;*****CODE2*****
BUFOFS	=0
CRUNCH	LDX TXTPTR
	LDY #4
	STY DORES
KLOOP	LDA BUFOFS,X
	BPL CMPSPC
	CMP #PI
	BEQ STUFFH
	INX
	BNE KLOOP
CMPSPC	CMP #' 
	BEQ STUFFH
	STA ENDCHR
	CMP #34
	BEQ STRNG
	BIT DORES
	BVS STUFFH
	CMP #'?
	BNE KLOOP1
	LDA #PRINTK
	BNE STUFFH
KLOOP1	CMP #'0
	BCC MUSTCR
	CMP #60
	BCC STUFFH
MUSTCR	STY BUFPTR
	LDY #0
	STY COUNT
	DEY
	STX TXTPTR
	DEX
RESER	INY
RESPUL	INX
RESCON	LDA BUFOFS,X
	CMP #' 
	BEQ RESPUL
	SEC
	SBC RESLST,Y
	BEQ RESER
	CMP #128
	BNE NTHIS
	ORA COUNT
GETBPT	LDY BUFPTR
STUFFH	INX
	INY
	STA BUF-5,Y
	LDA BUF-5,Y
	BEQ CRDONE
	SEC
	SBC #':
	BEQ COLIS
	CMP #DATATK-$3A
	BNE NODATT
COLIS	STA DORES
NODATT	SEC
	SBC #REMTK-$3A
	BNE KLOOP
	STA ENDCHR
STR1	LDA BUFOFS,X
	BEQ STUFFH
	CMP ENDCHR
	BEQ STUFFH
STRNG	INY
	STA BUF-5,Y
	INX
	BNE STR1
NTHIS	LDX TXTPTR
	INC COUNT
.SKI 3
;LOOK FOR END OF RESERVED WORD
NTHIS1	INY
	LDA RESLST-1,Y
	BPL NTHIS1
.SKI 3
;LOOK FOR END OF RESLST
	LDA RESLST,Y
	BNE RESCON
	LDA BUFOFS,X
	BPL GETBPT
.SKI 3
CRDONE	STA BUF-3,Y
ZZ1	=BUF-1
	LDA #<ZZ1
	STA TXTPTR
	RTS
FNDLIN	LDA TXTTAB
	LDX TXTTAB+1
FNDLNC	LDY #1
	STA LOWTR
	STX LOWTR+1
	LDA (LOWTR)Y
	BEQ FLINRT
	INY
	INY
	LDA LINNUM+1
	CMP (LOWTR)Y
	BCC FLNRTS
	BEQ FNDLO1
	DEY
	BNE AFFRTS
FNDLO1	LDA LINNUM
	DEY
	CMP (LOWTR)Y
	BCC FLNRTS
	BEQ FLNRTS
AFFRTS	DEY
	LDA (LOWTR)Y
	TAX
	DEY
	LDA (LOWTR)Y
	BCS FNDLNC
FLINRT	CLC
FLNRTS	RTS
SCRATH	BNE FLNRTS
SCRTCH	LDA #0
	TAY
	STA (TXTTAB)Y
	INY
	STA (TXTTAB)Y
	LDA TXTTAB
	ADC #2
	STA VARTAB
	LDA TXTTAB+1
	ADC #0
	STA VARTAB+1
RUNC	JSR STXTPT
CLEARC	LDA MEMSIZ
	LDY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	JSR CCALL
	LDA VARTAB
	LDY VARTAB+1
	STA ARYTAB
	STY ARYTAB+1
	STA STREND
	STY STREND+1
FLOAD	JSR RESTOR
STKINI	LDX #TEMPST
	STX TEMPPT
	PLA
;       STA STKEND
	TAY
	PLA
;       STA STKEND+1
	LDX #STKEND-257
	TXS
	PHA
	TYA
	PHA
	LDA #0
	STA OLDTXT+1
	STA SUBFLG
	RTS
	.BYT $51
STKRTS	RTS
STXTPT	CLC
	LDA TXTTAB
	ADC #255
	STA TXTPTR
	LDA TXTTAB+1
	ADC #255
	STA TXTPTR+1
	RTS
.FIL CODE3
