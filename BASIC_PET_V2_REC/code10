.PAG 'CODE10'
ISVAR	JSR PTRGET
ISVRET	STA FACMO
	STY FACLO
	LDA VARNAM
	LDY VARNAM+1
	LDX VALTYP
	BEQ GOOO
	LDX #0
	STX FACOV
	BIT FACLO
	BPL STRRTS
	CMP #'T
	BNE STRRTS
	CPY #$C9
	BNE STRRTS
	JSR GETTIM
	STY TENEXP
	DEY
	STY FBUFPT
	LDY #6
	STY DECCNT
	LDY #FDCEND-FOUTBL
	JSR FOUTIM
	JMP TIMSTR
STRRTS	RTS
GOOO	LDX INTFLG
	BPL GOOOOO
	LDY #0
	LDA (FACMO)Y
	TAX
	INY
	LDA (FACMO)Y
	TAY
	TXA
	JMP GIVAYF
GOOOOO	BIT FACLO       ;AN ARRAY?
	BPL GOMOVF
	CMP #'T
	BNE QSTATV
	CPY #'I
	BNE GOMOVF
	JSR GETTIM
	TYA
	LDX #160
	JMP FLOATB
ZZ7=CTIMR-2
GETTIM	LDA #<ZZ7
	LDY #>ZZ7
	SEI
	JSR MOVFM
	CLI
	STY FACHO
	RTS
QSTATV	CMP #'S
	BNE GOMOVF
	CPY #'T
	BNE GOMOVF
	LDA CSTAT
	JMP FLOAT
GOMOVF	LDA FACMO
	LDY FACMO+1
	JMP MOVFM
ISFUN	ASL A
	PHA
	TAX
	JSR CHRGET
	CPX #LASNUM+LASNUM-255
	BCC OKNORM
	JSR CHKOPN
	JSR FRMEVL
	JSR CHKCOM
	JSR CHKSTR
	PLA
	TAX
	LDA FACMO+1
	PHA
	LDA FACMO
	PHA
	TXA
	PHA
	JSR GETBYT
	PLA
	TAY
	TXA
	PHA
	JMP FINGO
OKNORM	JSR PARCHK
	PLA
	TAY
FINGO	LDA FUNDSP-ONEFUN-ONEFUN+256,Y
	STA JMPER+1
	LDA FUNDSP-ONEFUN-ONEFUN+257,Y
	STA JMPER+2
	JSR JMPER
	JMP CHKNUM
OROP	LDY #255
	.BYT $2C
ANDOP	LDY #0
	STY COUNT
	JSR AYINT
	LDA FACMO
	EOR COUNT
	STA INTEGR
	LDA FACLO
	EOR COUNT
	STA INTEGR+1
	JSR MOVFA
	JSR AYINT
	LDA FACLO
	EOR COUNT
	AND INTEGR+1
	EOR COUNT
	TAY
	LDA FACMO
	EOR COUNT
	AND INTEGR
	EOR COUNT
	JMP GIVAYF
DOREL	JSR CHKVAL
	BCS STRCMP
	LDA ARGSGN
	ORA #127
	AND ARGHO
	STA ARGHO
	LDA #<ARGEXP
	LDY #>ARGEXP
	JSR FCOMP
	TAX
	JMP QCOMP
STRCMP	LDA #0
	STA VALTYP
	DEC OPMASK
	JSR FREFAC
	STA DSCTMP
	STX DSCTMP+1
	STY DSCTMP+2
	LDA ARGMO
	LDY ARGMO+1
	JSR FRETMP
	STX ARGMO
	STY ARGMO+1
	TAX
	SEC
	SBC DSCTMP
	BEQ STASGN
	LDA #1
	BCC STASGN
	LDX DSCTMP
	LDA #$FF
STASGN	STA FACSGN
	LDY #255
	INX
NXTCMP	INY
	DEX
	BNE GETCMP
	LDX FACSGN
QCOMP	BMI DOCMP
	CLC
	BCC DOCMP
GETCMP	LDA (ARGMO)Y
.FIL CODE11
