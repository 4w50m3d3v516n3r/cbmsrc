	.nam  C/128 NORWAY NATIONAL EDITOR
	.subttl *** COPYRIGHT (C) 1987  by  COMMODORE BUSINESS MACHINES, INC. ***
	.forml 60

country	= $05		;NORWAY		p/n 325177-03  04/08/87  F.Bowen
checksum= $11		;character ROM checksum
chr_sig	= $3d99		;character ROM signature
rom_sig	= $4b68		;ROM signature


;  ******************************************************************
;  *                        //                                      *
;  *        CCCCCCC        //    111     2222222     8888888        *
;  *       CCC   CCC      //    1111    222   222   888   888       *
;  *      CCC            //      111         222    888   888       *
;  *      CCC           //       111       222        88888         *
;  *      CCC          //        111     222        888   888       *
;  *       CCC   CCC  //         111    222   222   888   888       *
;  *        CCCCCCC  //        1111111  222222222    8888888        *
;  *                //                                              * 
;  *                                                                * 
;  * EEEEEEEEE  DDDDDDDD    IIIII TTTTTTTTT   OOOOOOO    RRRRRRRR   * 
;  * EEE        DDD   DDD    III     TTT     OOO   OOO   RRR   RRR  * 
;  * EEE        DDD    DDD   III     TTT    OOO     OOO  RRR   RRR  * 
;  * EEEEEE     DDD    DDD   III     TTT    OOO     OOO  RRRRRRRR   * 
;  * EEE        DDD    DDD   III     TTT    OOO     OOO  RRR RRR    * 
;  * EEE        DDD   DDD    III     TTT     OOO   OOO   RRR  RRR   * 
;  * EEEEEEEEE  DDDDDDDD    IIIII    TTT      OOOOOOO    RRR   RRR  * 
;  *                                                                * 
;  *                                                                * 
;  *    COPYRIGHT (C)1987  BY   COMMODORE BUSINESS MACHINES, INC.   * 
;  *                                                                * 
;  ****************************************************************** 
  
  
  
;  ****************************************************************** 
;  *                                                                * 
;  * THIS LISTING CONTAINS CONFIDENTIAL AND PROPRIETARY INFORMATION *
;  * OF CBM, INC.   REPRODUCTION,  DISSEMINATION  OR  DISCLOSURE TO *
;  * OTHERS WITHOUT EXPRESS WRITTEN PERMISSION IS PROHIBITED.  THIS *
;  * SOFTWARE IS INTENDED FOR USE IN  COMMODORE C/128 SYSTEMS ONLY. *
;  *                                                                * 
;  *  THE  INFORMATION  IN  THIS  DOCUMENT  IS  SUBJECT  TO CHANGE  * 
;  *                       WITHOUT NOTICE.                          * 
;  *                                                                * 
;  *  NO  RESPONSIBILITY  IS  ASSUMED  FOR THE RELIABILITY OF THIS  * 
;  *                          SOFTWARE.                             * 
;  *                                                                * 
;  ****************************************************************** 
  
	.page










;  ****************************************************************** 
;  *                                                                * 
;  * THIS VERSION OF THE COMMODORE SCREEN EDITOR CONTAINS  SPECIFIC *
;  * MODIFICATIONS FOR THE IMPLEMENTATION OF THE FOLLOWING NATIONAL *
;  * KEYBOARD AND SCREEN EDITOR:                                    * 
;  *								    *
;  *         *   *    ***    ****    *    *     *    *   *          *
;  *         **  *   *   *   *   *   *    *    * *    * *           *
;  *         * * *   *   *   ****    * ** *   *****    *            *
;  *         *  **   *   *   *  *    **  **   *   *    *            *
;  *         *   *    ***    *   *   *    *   *   *    *            *
;  *                                                                * 
;  *		 THE ORIGINAL VERSION HEREIN IS BY:                 *
;  *	    DR. KARL BENDER, WALTER SCHMITZ, and FRED BOWEN	    *
;  *                                                                * 
;  ****************************************************************** 



;.end
	.include sysdoc
	.include relnotes
	.include declare
	.include entries

	* = $c000

.editor_check_begin

	.include ed1		;contains NATIONAL patches (4 places)
	.include ed2
	.include ed3		;contains NATIONAL patches (1 place)
	.include ed4
	.include ed5
	.include ed6
	.include routines
	.include ed7		;contains an ORG $FA80
	.include patches	;contains an ORG $CF00 (& upper limit @ $CF7F)

;	*****   ROM SPACE FROM  $FC80 THRU $FEFF IS RESERVED FOR   *****
;	*****   NATIONAL KEYBOARD DRIVERS AND ASSOCIATED TABLES.   *****

; 	.include <nation>.src	;contains an ORG $FC80 for national driver & tables

;	.end
	.page
	.subttl  C/128 NORWAY KEYBOARD DRIVER (01/15/86)

	* = $CFF8	;reserved ROM signature area

	.byte checksum
	.byte country
	.word chr_sig
	.word rom_sig


	* = $FC80	;reserved ROM area

nat_init
	sta dk_flag	;'cint' patch (.a=0)
	sta sidreg+24
	rts


nat_scnkey
	bit dk_flag
	bmi do_scan	;...branch if national keys disabled

	lda shflag	;test 'CAPS LOCK' for keyboard selection
	and #$10
	beq 10$		;...branch if up- use ASCII
	lda decode+1
	cmp #>nat_mode1
	beq do_scan	;...branch if national keys already setup
	lda #<nat_decode
	ldy #>nat_decode
	bne switch	;...else setup national keys

10$	lda decode+1
	cmp #>mode1
	beq do_scan	;...branch if ascii keys already setup
	lda #<keycod	;...else setup ascii keys
	ldy #>keycod

switch	sta keytab
	sty keytab+1
	ldy #11		;point keyscan to decode tables
10$	lda (keytab),y
	sta decode,y
	dey
	bpl 10$

	iny
	sty dk_flag	;clear any pending dead keys
	php
	sei
	jsr init80	;move new character set to 8563 RAM
	plp
	rts

do_scan
	jmp scnkey	;resume normal keyscan

	.page
nat_keychk
	ldx decode+1
	cpx #>nat_mode1
	bne 70$		;...branch to normal keyscan if ASCII mode
	ldx dk_flag
	bmi 70$		;...branch to normal keyscan if national scan disabled
	beq 25$		;...branch if previous key was not a dead key

	ldy deadp,x	;previous key was a dead key- index in .x
	dex
10$	dey
	pha		;save .a (current character)
	tya
	cmp deadp,x	;look to see if index (.y) is out of range
	pla		;restore current character
	bcc 20$		;...branch if not in table (out of range)
	cmp oldead-1,y	;look to see if current key can be substituted
	bne 10$		;...branch if not

	lda nwdead-1,y	;substitute it
20$	pha
	and #$7f	;weed out control codes
	cmp #$20
	pla
	bcc 45$		;...branch if control code & ignore it

25$	ldx #5
30$	cmp dktbl-1,x	;look to see if current key is a dead key
	beq 40$		;...branch if it is
	dex
	bne 30$

40$	stx dk_flag	;save for next time...
	cpx #$00
	beq 70$		;if current key is not a dead key we are done

	tay		;save dead key code
	bit insflg	;cannot display dead key if auto-insert mode active
	bmi 45$		;...branch if active
	bit mode	;which screen (40/80) is active?
	bpl 50$		;...branch if 40

	ldx #10
	jsr vdcin	;read 8563 register 10 to determine if cursor is on
	and #$40
	bne 60$		;...branch if cursor is on
45$	rts		;...else return & buffer nothing (dead key flag is set)

50$	lda blnsw
	bne 45$		;...branch if cursor is off

60$	tya		;display dead key using screen code
	ora #$40
	and #$7f	;convert dead key character to screen code
	jmp displc	;put character on screen at cursor without moving cursor
			;and without buffering dead key (dead key flag is set)


70$	ldx shflag	;setup registers as normal
	ldy lstx
	jmp (keychk)	;resume normal keyscan thru indirect

	.page
	.subttl  NORWAY NATIONAL KEYBOARD TABLES

nat_mode1		;unshifted keys
	.byte $14,$0d,$1d,$88,$85,$86,$87,$11,$33,$57,$41,$34,$5a,$53,$45,$01
	.byte $35,$52,$44,$36,$43,$46,$54,$58,$37,$59,$47,$38,$42,$48,$55,$56
	.byte $39,$49,$4a,$30,$4d,$4b,$4f,$4e,$2b,$50,$4c,$2d,$2e,$5c,$5d,$2c
	.byte $3a,$2a,$5b,$13,$01,$3d,$5e,$2f,$31,$5f,$04,$32,$20,$02,$51,$03
	.byte $84,$38,$35,$09,$32,$34,$37,$31,$1b,$2b,$2d,$0a,$0d,$36,$39,$33
	.byte $08,$30,$2e,$91,$11,$9d,$1d,$ff,$ff

nat_mode2		;shifted keys
	.byte $94,$8d,$9d,$8c,$89,$8a,$8b
	.byte $91,$23,$d7,$c1,$24,$da,$d3,$c5,$01,$25,$d2,$c4,$26,$c3,$c6,$d4
	.byte $d8,$27,$d9,$c7,$28,$c2,$c8,$d5,$d6,$29,$c9,$ca,$30,$cd,$cb,$cf
	.byte $ce,$db,$d0,$cc,$dd,$3e,$dc,$dd,$3c,$3b,$c0,$db,$93,$01,$40,$de
	.byte $3f,$21,$5f,$04,$22,$a0,$02,$d1,$83,$84,$38,$35,$18,$32,$34,$37
	.byte $31,$1b,$2b,$2d,$0a,$8d,$36,$39,$33,$08,$30,$2e,$91,$11,$9d,$1d
	.byte $ff,$ff

nat_mode3		;C= keys
	.byte $94,$8d,$9d,$8c,$89,$8a,$8b,$91,$96,$b3,$b0,$97,$ad,$ae
	.byte $b1,$01,$98,$b2,$ac,$99,$bc,$bb,$a3,$bd,$9a,$b7,$a5,$9b,$bf,$b4
	.byte $b8,$be,$29,$a2,$b5,$30,$a7,$a1,$b9,$aa,$a6,$af,$b6,$dc,$3e,$ba
	.byte $a4,$3c,$a8,$df,$a9,$93,$01,$40,$de,$3f,$81,$5f,$04,$95,$a0,$02
	.byte $ab,$03,$84,$38,$35,$18,$32,$34,$37,$31,$1b,$2b,$2d,$0a,$8d,$36
	.byte $39,$33,$08,$30,$2e,$91,$11,$9d,$1d,$ff,$ff

nat_decode
	.word nat_mode1, nat_mode2, nat_mode3, mode4
	.word nat_mode1, nat_mode1

	.page
dktbl	.byte $ff,$ff,$ff,$ff,$ff		;dead keys (none)

deadp	.byte $ff,$ff,$ff,$ff,$ff,$ff		;pointers to 'build' keys

oldead	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;new key tables
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;unused
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff
	.byte $ff,$ff,$ff

nwdead	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;substitute key tables
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff	;unused
	.byte $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff
	.byte $ff,$ff,$ff

	.page

; NATIONAL patch code.	The following area contains patches specifically
;			related to national keyboard implementations.
;
;   (patch first included in part #315078-02,  10/01/85   F.Bowen)

ascii_din
	lda r6510	;scan for ASCII/DIN key (a.k.a. CAPS LOCK)
	and #$40	;P6 is it
	eor #$40	;invert sense.....up=0, dwn=1
	lsr a
	lsr a
	sta shflag	;end of special abrieviated keyscan
	rts


.editor_check_top

	.subttl NORWAY EDITOR CROSS REFERENCE
	.end
