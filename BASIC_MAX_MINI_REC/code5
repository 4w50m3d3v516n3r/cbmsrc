.PAG 'CODE5'
	STA TXTPTR
	LDA LOWTR+1
	SBC #0
	STA TXTPTR+1
GORTS	RTS
RETURN	BNE GORTS
	LDA #255
	STA FORPNT+1
	JSR FNDFOR
	TXS
	CMP #GOSUTK
	BEQ RETU1
	LDX #ERRRG
	.BYT $2C
USERR	LDX #ERRUS
	JMP ERROR
SNERR2	JMP SNERR
RETU1	PLA
	PLA
	STA CURLIN
	PLA
	STA CURLIN+1
	PLA
	STA TXTPTR
	PLA
	STA TXTPTR+1
DATA	JSR DATAN
ADDON	TYA
	CLC
	ADC TXTPTR
	STA TXTPTR
	BCC REMRTS
	INC TXTPTR+1
REMRTS	RTS
DATAN	LDX #':
	.BYT $2C
REMN	LDX #0
	STX CHARAC
	LDY #0
	STY ENDCHR
EXCHQT	LDA ENDCHR
	LDX CHARAC
	STA CHARAC
	STX ENDCHR
REMER	LDA (TXTPTR)Y
	BEQ REMRTS
	CMP ENDCHR
	BEQ REMRTS
	INY
	CMP #34
	BNE REMER
	BEQ EXCHQT
IF	JSR FRMEVL
	BIT VALTYP
	BMI SNERR2
	JSR CHRGOT
	CMP #GOTOTK
	BEQ OKGOTO
	LDA #THENTK
	JSR SYNCHR
OKGOTO	LDA FACEXP
	BNE DOCOND
REM	JSR REMN
	BEQ ADDON
DOCOND	JSR CHRGOT
	BCS DOCO
	JMP GOTO
DOCO	JMP GONE3
ONGRTS	RTS
LINGET	LDX #0
	STX LINNUM
	STX LINNUM+1
MORLIN	BCS ONGRTS
	SBC #$2F
	STA CHARAC
	LDA LINNUM+1
	STA INDEX
	CMP #25
	BCS SNERR2
	LDA LINNUM
	ASL A
	ROL INDEX
	ASL A
	ROL INDEX
	ADC LINNUM
	STA LINNUM
	LDA INDEX
	ADC LINNUM+1
	STA LINNUM+1
	ASL LINNUM
	ROL LINNUM+1
	LDA LINNUM
	ADC CHARAC
	STA LINNUM
	BCC NXTLGC
	INC LINNUM+1
NXTLGC	JSR CHRGET
	JMP MORLIN
LET	JSR PTRGET
	STA FORPNT
	STY FORPNT+1
	LDA #$A4
	JSR SYNCHR
	LDA VALTYP
	PHA
	JSR FRMEVL
	PLA
	ROL A
	JSR CHKVAL
	BNE COPSTR
QINTGR	JMP MOVVF
COPSTR
INPCOM	LDY FORPNT+1
	CPY #>ZERO
	BNE GETSPT
	JMP SNERR
.END
